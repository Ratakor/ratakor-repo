# Maintainer: Ratakor <ratakor@disroot.org>

pkgname=gruvbox-gtk-theme
_pkgname=Gruvbox-GTK-Theme
pkgver=r50.c7a85272
pkgrel=1
pkgdesc="A GTK theme based on the Gruvbox colour palette"
arch=(any)
url="https://github.com/Fausto-Korpsvart/$_pkgname"
license=('GPL3')
depends=('gtk-engine-murrine')
provides=("$pkgname")
conflicts=("$pkgname")
source=("git+$url#commit=c7a8527")
sha256sums=('SKIP')

pkgver() {
	cd "$_pkgname"
	printf 'r%s.%s' \
		"$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	install -d "$pkgdir/usr/share/themes/${pkgname%%-*}-dark"
	install -d "$pkgdir/usr/share/themes/${pkgname%%-*}-light"
	cd "$srcdir/$_pkgname"
	cp -r themes/Gruvbox-Dark-BL/* "$pkgdir/usr/share/themes/${pkgname%%-*}-dark"
	cp -r themes/Gruvbox-Light-BL/* "$pkgdir/usr/share/themes/${pkgname%%-*}-light"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
