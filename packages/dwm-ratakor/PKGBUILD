# Maintainer: Ratakor <ratakor@disroot.org>

pkgname=dwm-ratakor
_pkgname="${pkgname%-ratakor}"
pkgver=r45.299526e
pkgrel=1
pkgdesc="Ratakor's build of dwm"
arch=('x86_64')
url="https://github.com/ratakor/$_pkgname"
license=('MIT')
depends=('libx11' 'libxft' 'libxinerama' 'freetype2')
optdepends=('dwmblocks-ratakor: status bar'
            'st-ratakor: terminal emulator'
            'dmenu-ratakor: application launcher'
            'picom: compositor for transparency'
            'xorg-xrdb: Xresource for setting theme'
            'sxhkd: hotkey daemon (there is no default binding for spawning st or dmenu)')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+$url")
sha256sums=('SKIP')

pkgver() {
	cd "$_pkgname"
	printf 'r%s.%s' \
		"$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd "$srcdir/$_pkgname"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
	make DESTDIR="$pkgdir" PREFIX="/usr" install
}
